apply plugin: 'com.moowork.grunt'

def webappOutputDir = 'build/'
def webappTestOutputDir = 'build/karma/'
def gruntBinary = rootProject.projectDir.path + '/node_modules/grunt-cli/bin/grunt'

grunt {
    colors = System.properties['gruntColors'] ? System.properties['gruntColors'].toBoolean() : true
}

node {
    version = "${nodeJsVersion}"
    npmVersion = "${npmVersion}"
    download = true
    distBaseUrl = 'https://build-inera.nordicmedtest.se/node/'
    workDir = file("${projectDir}/.gradle/nodejs")
    npmWorkDir = file("${projectDir}/.gradle/npm")
    nodeModulesDir = rootProject.projectDir
}

sourceSets {
    main {
        resources {
            exclude '**/*.scss'
            exclude '**/*.spec.js'
        }
    }
}

task jsTests(type: NodeTask, dependsOn: [npmInstall]) {
    inputs.dir("$projectDir/src/main/resources")
    inputs.files(fileTree(projectDir).include('*.js'))
    inputs.files(fileTree(projectDir).include('*.json'))
    inputs.files(configurations.compile)
    outputs.dir(webappTestOutputDir)

    script = file(gruntBinary)
    args = ['test', "--outputDir=${webappTestOutputDir}", "--base=${projectDir}", "--npmDir=${rootProject.projectDir}",
            '--run-coverage=' + (project.findProperty('codeCoverage') != null ? 'true' : '')]
}

test.dependsOn(jsTests)

task jsInclude(type: NodeTask, dependsOn: [npmInstall]) {
    inputs.dir("$projectDir/src/main/resources")
    inputs.files(fileTree(projectDir).include('*.js'))
    inputs.files(fileTree(projectDir).include('*.json'))
    inputs.files(configurations.compile)
    outputs.dir(webappOutputDir)

    script = file(gruntBinary)
    args = ['default', "--outputDir=${webappOutputDir}", "--base=${projectDir}", "--npmDir=${rootProject.projectDir}"]
}

jar.dependsOn(jsInclude)

task unzipBuildTools(type: Copy) {
    into "$buildDir/build-tools/"
    from {
        def intygJar = project.rootProject.buildscript.configurations.classpath.find {
            it.name.contains("se.inera.intyg.plugin.common")
        }
        zipTree(intygJar)
    }
}

task jsCodeQuality(type: NodeTask, dependsOn: [npmInstall, unzipBuildTools]) {
    outputs.upToDateWhen { false }

    script = file(gruntBinary)
    args = ['lint', "--base=${projectDir}", "--npmDir=${rootProject.projectDir}"]
}

checkstyleMain.dependsOn jsCodeQuality
jsCodeQuality.onlyIf { project.hasProperty('codeQuality') }
